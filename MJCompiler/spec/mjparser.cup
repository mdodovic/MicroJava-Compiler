
package rs.ac.bg.etf.pp1;

import java_cup.runtime.*;

import org.apache.log4j.*;
import rs.ac.bg.etf.pp1.ast.*;

parser code {:

	Logger log = Logger.getLogger(getClass());
	
	boolean errorDetected;
   
    // slede redefinisani metodi za prijavu gresaka radi izmene teksta poruke
     
    public void report_fatal_error(String message, Object info) throws java.lang.Exception {
      done_parsing();
      report_error(message, info);
    }
  
    public void syntax_error(Symbol cur_token) {
        report_error("\nSintaksna greska", cur_token);
    }
  
    public void unrecovered_syntax_error(Symbol cur_token) throws java.lang.Exception {
        report_fatal_error("Fatalna greska, parsiranje se ne moze nastaviti", cur_token);
    }

    public void report_error(String message, Object info) {
		errorDetected = true;
    	StringBuilder msg = new StringBuilder(message); 
    	if (info instanceof Symbol)
            msg.append (" na liniji ").append(((Symbol)info).left);
        log.error(msg.toString());
    }


:}

init with{:
	errorDetected = false;

:}

scan with {:
	Symbol s = this.getScanner().next_token();
	if (s != null && s.value != null) 
		log.info(s.toString() + " " + s.value.toString());
	return s;
:}

/* terminals */

terminal PROG; /* "program" */ 
terminal CONST; /* "const" */

terminal SEMI; /* ";" */
terminal COMMA; /* "," */

terminal CHAR_CONST; /* char value: 'a', ... */ 
terminal BOOL_CONST; /* bool value: true/false*/
terminal Integer NUMBER; /* integer value: 0, ... */

terminal LEFT_SQUARE_BRACKET, RIGHT_SQUARE_BRACKET; /* "[""]" */


terminal PRINT, RETURN, PLUS, VOID;
terminal String IDENT;
terminal IF, ELSE;

terminal READ;
terminal GOTO, DO, WHILE;
terminal INC, DEC, MINUS, MULTIPLY, DIVIDE, MODUO;
terminal AND, OR;
terminal COLON, ASSIGNMENT, EXTENDS, NEW;
terminal ENUM, RECORD;
terminal CLASS, SUPER, THIS, POINT;

terminal BREAK, CONTINUE;
terminal EQUAL, NOT_EQUAL, LESS, GREATER, GREATER_EQUAL, LESS_EQUAL;
terminal LEFT_ROUND_BRACKET, RIGHT_ROUND_BRACKET;
terminal LEFT_CURLY_BRACKET, RIGHT_CURLY_BRACKET; 



nonterminal MethodDeclList;
nonterminal FormPars, StatementList, FormalParamList, FormalParamDecl, Statement;
nonterminal Addop;
nonterminal ActualPars, ActualParamList;
nonterminal Matched, Unmatched;

nonterminal rs.etf.pp1.symboltable.concepts.Obj MethodDecl, MethodTypeName;

nonterminal rs.etf.pp1.symboltable.concepts.Obj Designator;

nonterminal rs.etf.pp1.symboltable.concepts.Struct Term, Factor, Expr;

/* NON terminals */
nonterminal rs.etf.pp1.symboltable.concepts.Obj ProgName, Program; /* Program = "program" ProgName (...) part, fetch program name and save to progName */
nonterminal AllDeclarationsList; /* this will collect all constants, (global) variables, classes and records declarations */

nonterminal ConstantDeclaration; /* "const" (...) this will collect all ConstDecls */
nonterminal ConstDeclarationType; /* type of constant */
nonterminal ConstValue; /* value of constant */
nonterminal MoreSingleLineConstDeclarations; /* this will collect all constants between initial constant and ; `"const" Type ident "=" value` (. "," ident "=" value .) `";"` */

nonterminal VarDecl; /* this will collect all global variables */
nonterminal VarDeclarationType; /* type of global variables, this will be common type for all variables in the row */ 
nonterminal VarDeclList; /* this will collect all variable names, and divide them to not-last-one ant the last-one */
/* not-last-one (NotLastVarDecl) and last-one (LastVarDecl) nonterminals are differed because of error handling: if the error happened all tokens except "," and ";" should be consumed.
   NotLastVarDecl will cover consuming to the "," while the LastVarDecl will cover consuming to the ";"
 */
nonterminal NotLastVarDecl; /* this will collect all variable names until the last, it can be like varName or varName[] */
nonterminal LastVarDecl; /* this will collect the last variable name, it can be like varName or varName[]  */
nonterminal ArrayBrackets; /* this will represent [] declaration like int a[], and will contain the info about if the variable is array or not */

nonterminal rs.etf.pp1.symboltable.concepts.Struct Type; /* ident that represent the variable type. It can be int/bool/char or an arbitrary class name */


/* Grammar */

Program ::= (Program) PROG ProgName:p AllDeclarationsList LEFT_CURLY_BRACKET MethodDeclList RIGHT_CURLY_BRACKET;

ProgName ::= (ProgName) IDENT:progName;

AllDeclarationsList ::= (VarDeclarations) AllDeclarationsList VarDecl
						|
						(ConstDeclarations) AllDeclarationsList ConstantDeclaration
						|
						(NoDeclarations) /* epsilon */
						;

/* constants */

ConstantDeclaration ::= (ConstDecl) CONST ConstDeclarationType IDENT:constName ASSIGNMENT ConstValue:constValue MoreSingleLineConstDeclarations SEMI;

ConstDeclarationType ::= (ConstDeclarationType) Type:constType;

ConstValue ::= (BooleanValue) BOOL_CONST:constValue
			   |
			   (IntegerValue) NUMBER:constValue
			   |
			   (CharValue) CHAR_CONST:constValue
			   ;

MoreSingleLineConstDeclarations ::= (MoreConstDeclarations) MoreSingleLineConstDeclarations COMMA IDENT:constName ASSIGNMENT ConstValue:constValue
									|
									(NoMoreConstDeclarations) /* epsilon */
									;

/* global variables */

VarDecl ::= (VarDecl) VarDeclarationType VarDeclList;

VarDeclarationType ::= (VarDeclarationType) Type:varType;

VarDeclList ::= (VarDeclMultiple) NotLastVarDecl VarDeclList
				|
				(VarDeclLast) LastVarDecl
				;

NotLastVarDecl ::= (VarFromMultiplePart) IDENT:varName ArrayBrackets COMMA
   				   |
				   (ErrorInVarFromMultiplePart) error COMMA
				   ;

LastVarDecl ::= (VarFromLastPart) IDENT:varName ArrayBrackets SEMI
				|
				(ErrorInVarFromLastPart) error SEMI
				;

ArrayBrackets ::= (VariableIsArray) LEFT_SQUARE_BRACKET RIGHT_SQUARE_BRACKET
				  |
				  (VariableIsNotArray) /* epsilon */
				  ;

Type ::= (Type) IDENT:typeName;



MethodDeclList ::= (MethodDeclarations) MethodDeclList MethodDecl
				|
				(NoMethodDecl) /* epsilon */
				;

MethodDecl ::= (MethodDecl) MethodTypeName 
				LEFT_ROUND_BRACKET FormPars RIGHT_ROUND_BRACKET VarDeclList 
				LEFT_CURLY_BRACKET StatementList RIGHT_CURLY_BRACKET
				;

MethodTypeName ::= (MethodTypeName) Type:retType IDENT:methName;

FormPars ::= (FormParams) FormalParamList
			|
			(NoFormParam) /* epsilon */
			;
			
FormalParamList ::= (FormalParamDecls) FormalParamList COMMA FormalParamDecl
				|
				(SingleFormalParamDecls) FormalParamDecl
				;

StatementList ::= (Statements) StatementList Statement
				|
				(NoStmt) /* epsilon */
				;

Statement ::= (MatchedStmt) Matched
			  |
			  (UnmatchedStmt) Unmatched
  			  ;

Unmatched ::= (IfStatement) IF Expr Statement
			  |
			  (UnmatchedIfElse) IF Expr Matched ELSE Unmatched
			  ;

Matched ::= (Assignment) Designator:dest ASSIGNMENT Expr:e SEMI
			  |
			  (ErrorStmt) error SEMI:l
			  {: parser.report_error("Izvrsen oporavak do ; u liniji " + lleft, null); :}
			  |
			  (PrintStmt) PRINT LEFT_ROUND_BRACKET Expr RIGHT_ROUND_BRACKET SEMI
			  |
			  (ReturnExpr) RETURN Expr:t SEMI
			  |
			  (ReturnNoExpr) RETURN SEMI
			  |
			  (MatchedStatement) IF Expr Matched ELSE Matched
              |
			  (ProcCall) Designator:func LEFT_ROUND_BRACKET ActualPars RIGHT_ROUND_BRACKET SEMI
			  ;				

Expr ::= (AddExpt) Expr:te Addop Term:t
		|
		(TermExpr) Term:t
		;						

Term ::= (Term) Factor:t;

Factor ::= (Const) NUMBER
		|
		(Var) Designator:d
		|
		(FuncCall) Designator:func LEFT_ROUND_BRACKET ActualPars RIGHT_ROUND_BRACKET
		;
		
ActualPars ::= (Actuals) ActualParamList
			|
			(NoActuals) /* epsilon */
			;
			
ActualParamList ::= (ActualParams) ActualParamList COMMA Expr
				|
				(ActualParam) Expr
				;		
	
Designator ::= (Designator) IDENT:name;

Addop ::= (Addop) PLUS;
		